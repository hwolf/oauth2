/*
 * Copyright 2015 H. Wolf
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies { 
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE" 
        classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
    }
}

plugins {
    id 'com.coverity.ondemand' version '1.3.632'
    id 'org.unbroken-dome.test-sets' version '1.1.0'
    id 'info.solidsoft.pitest' version '1.1.4'
}

def javaProjects = subprojects

configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    
    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.2.5.RELEASE'
        }
        dependencies {
            dependencySet(group: 'com.google.guava', version: '18.0') {
                entry 'guava'
                entry 'guava-testlib'
            }
            dependencySet(group: 'org.springframework.security.oauth', version: '2.0.7.RELEASE') {
                entry 'spring-security-oauth2'
            }
            dependencySet(group: 'org.springframework.cloud', version: '1.0.3.RELEASE') {
                entry 'spring-cloud-starter-security'
                entry 'spring-cloud-starter-zuul'
            }
            dependency "org.codehaus.groovy:groovy-all:2.3.11"
            dependencySet(group: 'org.spockframework', version: '1.0-groovy-2.3') {
                entry 'spock-core'
                entry 'spock-spring'
            }
            dependency "org.gebish:geb-spock:0.12.2"
            dependencySet(group: 'org.seleniumhq.selenium', version: '2.47.1') {
                entry 'selenium-firefox-driver'
                entry 'selenium-support'
            }
        }
    }
    
    configurations {
        //all*.exclude module: 'commons-logging'
        all*.exclude module: "spring-boot-starter-tomcat"
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter"
        compile "com.google.guava:guava"

        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "org.codehaus.groovy:groovy-all"
        testCompile "org.spockframework:spock-core"
        testCompile "org.spockframework:spock-spring"
        //testCompile "org.assertj:assertj-core:3.0.0"
        testCompile "com.google.guava:guava-testlib"
    }

    jacoco {
        toolVersion = "0.7.5.201505241946"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    testSets {
        integrationTest { 
            dirName = 'test-integration'
        }
    }

    jar {
        version = ""
        manifest {
            attributes("Version": project.version)
        }
    }

    test {
        testLogging {
            exceptionFormat "full"
        }
        reports {
            html.enabled = false
            junitXml.destination = file("${buildDir}/test-results/${name}")
        }

        outputs.upToDateWhen { false }
    }
    
    integrationTest {
        testLogging {
            exceptionFormat "full"
        }
        reports {
            html.enabled = false
            junitXml.enabled = false
        }

        outputs.upToDateWhen { false }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    jacocoTestReport {
        reports {
            html.enabled = false
            xml.enabled = true
            csv.enabled = false
        }
    }

    //coverityCheck.dependsOn subprojects*.integrationTest
    coverityCheck.onlyIf { 
        project.hasProperty('runCoverity') && project.getProperty('runCoverity')
    }
}

task testRootReport(type: TestReport) {
    reportOn subprojects*.test
    destinationDir = file("${reporting.baseDir}/test")
    test.testLogging { 
        exceptionFormat "full" 
    }
}

task integrationTestRootReport(type: TestReport) {
    reportOn subprojects*.integrationTest
    destinationDir = file("${reporting.baseDir}/integrationTest")
    test.testLogging { 
        exceptionFormat "full" 
    }
}

task ciBuild {
    dependsOn allprojects*.build, 
        subprojects*.jacocoTestReport, 
        testRootReport, 
        integrationTestRootReport
}
