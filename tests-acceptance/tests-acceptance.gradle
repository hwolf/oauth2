/*
 * Copyright 2015 H. Wolf
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.wiredforcode.gradle.spawn.*

plugins {
    id "com.wiredforcode.spawn" version "0.6.0"
}

dependencies {
    testCompile "org.gebish:geb-spock"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver"
    testCompile "org.seleniumhq.selenium:selenium-support"
}

testSets {
    acceptanceTest {
        dirName = 'test-acceptance'
    }
}

acceptanceTest.dependsOn "startServerUi"
acceptanceTest.dependsOn "startServerAuth"
acceptanceTest.dependsOn "startServerResource"

acceptanceTest.finalizedBy "stopServerResource"
acceptanceTest.finalizedBy "stopServerAuth"
acceptanceTest.finalizedBy "stopServerUi"

task startServerAuth(type: SpawnProcessTask, dependsOn: ":server-auth:assemble") {
    command "java -jar -Dspring.profiles.active=dev,testdata ${rootDir}/server-auth/build/libs/server-auth.jar"
    ready 'Undertow started on port'
    directory "$buildDir/pids/server-auth"
}

startServerAuth.doFirst { mkdir "$buildDir/pids/server-auth" }

task stopServerAuth(type: KillProcessTask) {
    directory "$buildDir/pids/server-auth"
}

task startServerResource(type: SpawnProcessTask, dependsOn: ":server-example-resource:assemble") {
    command "java -jar ${rootDir}/server-example-resource/build/libs/server-example-resource.jar"
    ready 'Undertow started on port'
    directory "$buildDir/pids/server-example-resource"
}

startServerResource.doFirst { mkdir "$buildDir/pids/server-example-resource" }

task stopServerResource(type: KillProcessTask) {
    directory "$buildDir/pids/server-example-resource"
}

task startServerUi(type: SpawnProcessTask, dependsOn: ":server-example-ui:assemble") {
    command "java -jar ${rootDir}/server-example-ui/build/libs/server-example-ui.jar"
    ready 'Undertow started on port'
    directory "$buildDir/pids/server-example-ui"
}

startServerUi.doFirst { mkdir "$buildDir/pids/server-example-ui" }

task stopServerUi(type: KillProcessTask) {
    directory "$buildDir/pids/server-example-ui"
}
